apply plugin: 'com.android.application'

ext {
    appName = 'MatrixApp'
    AC_MODE_TEST = '0'
    AC_MODE_PRODUCTION = '1'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "ablecloud.matrix.app.demo"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        setProperty("archivesBaseName", "${project.getParent().getName()}-${versionName}")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField 'long', 'AC_MAJOR_ID', '3L'
        buildConfigField 'String', 'AC_MAJOR_NAME', '"ablecloud"'
        buildConfigField 'int', 'AC_MODE', AC_MODE_TEST
    }

    dataBinding {
        enabled true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug.${new Date().format('yyyyMMdd')}"
            resValue "string", "app_name", "${appName}@Debug"
        }
        release {
            resValue "string", "app_name", appName

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'

    // TODO Merge ultra-ptr into ablecloue-common
    compile(name: 'ablecloud-common-0.1-release', ext: 'aar')
    compile 'in.srain.cube:ultra-ptr:1.0.11'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.mobsandgeeks:android-saripaar:2.0.3'
}
