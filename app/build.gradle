import com.android.builder.core.BuilderConstants

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties
if (keystorePropertiesFile.exists()) {
    keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}


android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.accloud.ac_service_android_demo"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 126
        versionName "v1.2.6"

        setProperty("archivesBaseName", "${project.getParent().getName()}-${versionName}")
    }

    signingConfigs {
        release {
            if (keystoreProperties != null) {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    dataBinding {
        enabled true
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == BuilderConstants.RELEASE) {
            def assembleTask = variant.assemble
            assembleTask.doLast {
                copy {
                    from variant.outputs[0].outputFile
                    into '../'
                }
                println "Publish output file of task '${assembleTask.name}'"
            }
        }
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.mobsandgeeks:android-saripaar:2.0.3'

    compile(name: 'ablecloud-common-0.1-release', ext: 'aar')
    compile 'in.srain.cube:ultra-ptr:1.0.11'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
}
